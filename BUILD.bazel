load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@//lib:pkgbuild.bzl", "pkgbuild_tars")

# The resulting PKG should be installable by:
#
# sudo installer -pkg bazel-out/darwin-fastbuild/bin/macos-runner-{arch}.pkg -target /

# Test this payload with:
#     bazel build //:actions-runners && tar tzf bazel-bin/macos-runner-{arm64,amd64}.tar
# TODO: remove .dll files for smaller payload?

ARCH_DEP = {
    "amd64": { "d": "@actions_runner_tar_osx_x64//file", },
    "arm64": { "d": "@actions_runner_tar_osx_arm64//file", },
}

[pkg_tar(
    name = "actions-runner-{}".format(k),
    mode = "0755",
    package_dir = "/usr/local/actions-runner",
    deps = [ v["d"] ],
) for k,v in ARCH_DEP.items() ]

[pkgbuild_tars(
    name = "macos-runner-{}".format(k),
    package_name = "macos-runner-{}.pkg".format(k),
    identifier = "com.chickenandpork.macos-runner",
    tars = [":actions-runner-{}".format(k)],
    version = "${VERSION}",  # indirectly drawn from workspace_status based on git tag vX.Y.Z
) for k,v in ARCH_DEP.items() ]


[copy_file(
    name = "dist_pkg-{}".format(k),
    src = ":macos-runner-{}".format(k),
    out = ".distrib/macos-runner-{}.pkg".format(k),
    allow_symlink = True,
    is_executable = False,
) for k,v in ARCH_DEP.items() ]

filegroup(name="actions-runners", srcs = [ "dist_pkg-{}".format(k) for k in ARCH_DEP.keys() ])
